{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using ArbNumerics, DifferentialEquations, DataInterpolations, Format, Integrals, MCIntegration, NaturalUnits\n",
    "\n",
    "using CairoMakie, LaTeXStrings"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "48e46fdc",
   "metadata": {},
   "outputs": [],
   "source": [
    "set_theme!(theme_latexfonts())\n",
    "\n",
    "include(\"tool_script-directory.jl\")\n",
    "include(\"tool_script-geomspace.jl\")\n",
    "\n",
    "include(\"tool_script-collision_term_integrand.jl\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3481769e",
   "metadata": {},
   "outputs": [],
   "source": [
    "EU = GeV\n",
    "NU = NaturalUnit(EU)\n",
    "\n",
    "# arb_float = arb_float\n",
    "arb_float = identity"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "81313f7b",
   "metadata": {},
   "outputs": [],
   "source": [
    "a_ini = arb_float(1)\n",
    "H_ini = arb_float(2e-5) * NU.M_Pl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "adf73eb1",
   "metadata": {},
   "outputs": [],
   "source": [
    "save_plot_flag = false"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7fd00be0",
   "metadata": {},
   "outputs": [],
   "source": [
    "function evolution_system!(du_list, u_list, params, log_a)\n",
    "    n_inflaton_comoving = EU(exp(u_list[1]), 3)\n",
    "    ρ_total_comoving = EU(exp(u_list[2]), 4)\n",
    "    a = exp(log_a)\n",
    "\n",
    "    m_inflaton = params.m_inflaton\n",
    "    λ_inflaton_reheaton_reheaton = params.λ_inflaton_reheaton_reheaton\n",
    "\n",
    "    ρ_inflaton_comoving = min(m_inflaton * n_inflaton_comoving, ρ_total_comoving / a)\n",
    "    # ρ_reheaton_comoving = ρ_total_comoving - ρ_inflaton_comoving * a\n",
    "    n_inflaton_comoving = ρ_inflaton_comoving / m_inflaton\n",
    "\n",
    "    ρ_total = ρ_total_comoving / a^4\n",
    "    H = sqrt(ρ_total / (3 * NU.M_Pl^2))\n",
    "\n",
    "    d_n_inflaton_comoving_OVER_d_a = -(λ_inflaton_reheaton_reheaton^2 / (32 * π * m_inflaton * a * H)) * exp(\n",
    "        π * λ_inflaton_reheaton_reheaton^2 / (m_inflaton^4 * a^3 * H) * n_inflaton_comoving\n",
    "    ) * n_inflaton_comoving\n",
    "    d_ρ_total_comoving_OVER_d_a = ρ_inflaton_comoving\n",
    "\n",
    "    du_list[1] = d_n_inflaton_comoving_OVER_d_a * a / n_inflaton_comoving\n",
    "    du_list[2] = d_ρ_total_comoving_OVER_d_a * a / ρ_total_comoving\n",
    "\n",
    "    return du_list\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "29f427e3",
   "metadata": {},
   "outputs": [],
   "source": [
    "function solve_evolution_system(;\n",
    "    a_fin=arb_float(1e6),\n",
    "    m_inflaton=GeV(arb_float(1e13)),\n",
    "    λ_inflaton_reheaton_reheaton=GeV(arb_float(1e7)),\n",
    "    solver=Rosenbrock23(),\n",
    "    initial_condition_fixing=false\n",
    ")\n",
    "    n_inflaton_ini = 3 * NU.M_Pl^2 * H_ini^2 / m_inflaton\n",
    "    ρ_total_comoving = m_inflaton * n_inflaton_ini * a_ini^4\n",
    "\n",
    "    n_inflaton_ini = initial_condition_fixing ? min(\n",
    "        m_inflaton^4 * H_ini / (π * λ_inflaton_reheaton_reheaton^2) * log(\n",
    "            32 * π * m_inflaton * H_ini / λ_inflaton_reheaton_reheaton^2\n",
    "        ) * 5,\n",
    "        n_inflaton_ini\n",
    "    ) : n_inflaton_ini\n",
    "        \n",
    "    evolution_problem = ODEProblem(evolution_system!,\n",
    "        [\n",
    "            (log ∘ EUval)(EU, n_inflaton_ini),\n",
    "            (log ∘ EUval)(EU, ρ_total_comoving),\n",
    "        ],\n",
    "        (log(a_ini), log(a_fin)),\n",
    "        (\n",
    "            m_inflaton = m_inflaton,\n",
    "            λ_inflaton_reheaton_reheaton = λ_inflaton_reheaton_reheaton,\n",
    "        )\n",
    "    )\n",
    "    evolution_solution = solve(evolution_problem, solver)\n",
    "    evolution_solution.retcode == ReturnCode.Success || @warn \"ODE return code: $(evolution_solution.retcode)\"\n",
    "\n",
    "    return evolution_solution\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9bb4dbbf",
   "metadata": {},
   "outputs": [],
   "source": [
    "function f_reheaton(a, p_reheaton, m_inflaton, λ_inflaton_reheaton_reheaton, evolution_solution)\n",
    "    (m_inflaton / 2) * (a_ini / a) ≤ p_reheaton ≤ (m_inflaton / 2) || return arb_float(0)\n",
    "\n",
    "    a′ = (2 * p_reheaton * a) / m_inflaton\n",
    "\n",
    "    n′_inflaton_comoving_in_EU = (exp ∘ first ∘ evolution_solution ∘ log)(a′)\n",
    "    ρ′_total_comoving_in_EU = (exp ∘ last ∘ evolution_solution ∘ log)(a′)\n",
    "\n",
    "    n′_inflaton = EU(n′_inflaton_comoving_in_EU, 3) / a′^3\n",
    "    ρ′_total = EU(ρ′_total_comoving_in_EU, 4) / a′^4\n",
    "    \n",
    "    H′ = sqrt(ρ′_total / (3 * NU.M_Pl^2))\n",
    "\n",
    "    return exp(π * λ_inflaton_reheaton_reheaton^2 * n′_inflaton / (m_inflaton^4 * H′)) - 1\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4dbe0d62",
   "metadata": {},
   "outputs": [],
   "source": [
    "function collision_term_h(a, kₕ, m_inflaton, λ_inflaton_reheaton_reheaton, evolution_solution;\n",
    "    abstol=0, reltol=1e-10\n",
    ")\n",
    "    a ≤ a_ini && return EU(arb_float(0))\n",
    "    kₕ ≤ m_inflaton || return EU(arb_float(0))\n",
    "    \n",
    "    prefactor = inv(64 * (2 * π)^4 * kₕ)\n",
    "    function integrand(Eq, p₁)\n",
    "        sₘₐₓ = 4 * min(kₕ * (Eq - kₕ), p₁ * (Eq - p₁))\n",
    "\n",
    "        result = CT_integrand(kₕ, Eq, p₁, sₘₐₓ) / (Eq - kₕ)\n",
    "        result *= f_reheaton(a, p₁, m_inflaton, λ_inflaton_reheaton_reheaton, evolution_solution)\n",
    "        result *= f_reheaton(a, Eq - p₁, m_inflaton, λ_inflaton_reheaton_reheaton, evolution_solution)\n",
    "\n",
    "        return result\n",
    "    end\n",
    "    function numerical_integrand(x_list, _)\n",
    "        x_Eq, x_p₁ = x_list\n",
    "\n",
    "        Eqₘᵢₙ = max(kₕ, m_inflaton * (a_ini / a))\n",
    "        Eqₘₐₓ = m_inflaton\n",
    "        Eqₘᵢₙ < Eqₘₐₓ || return arb_float(0)\n",
    "        Eq = Eqₘᵢₙ + (Eqₘₐₓ - Eqₘᵢₙ) * x_Eq\n",
    "\n",
    "        p₁_length = min(m_inflaton - Eq, Eq - m_inflaton * (a_ini / a))\n",
    "        EUval(EU, p₁_length) ≤ arb_float(0) && return arb_float(0)\n",
    "\n",
    "        p₁ₘᵢₙ = (Eq - p₁_length) / 2\n",
    "        # p₁ₘₐₓ = (Eq + p₁_length) / 2\n",
    "        p₁ = p₁ₘᵢₙ + p₁_length * x_p₁\n",
    "\n",
    "        # open(\"tmp.log\", \"a+\") do io\n",
    "        #     write(io,\n",
    "        #         \"x: $(x_list)\\nintegrand: $(EUval(EU, integrand(s, Eq, p₁) * sₘₐₓ * (Eqₘₐₓ - Eqₘᵢₙ) * p₁_length))\\n\\n\"\n",
    "        #         # \"a = $(a)\\ns = $(s)\\nEq = $(Eq)\\np₁ = $(p₁)\\nintegrand = $(integrand(s, Eq, p₁))\\n\\n\";\n",
    "        #     )\n",
    "        # end\n",
    "\n",
    "        # return EUval(EU, integrand(s, Eq, p₁) * sₘₐₓ * (Eqₘₐₓ - Eqₘᵢₙ) * (p₁ₘₐₓ - p₁ₘᵢₙ))\n",
    "        return EUval(EU, integrand(Eq, p₁) * (Eqₘₐₓ - Eqₘᵢₙ) * p₁_length)\n",
    "    end\n",
    "\n",
    "    integral_problem = IntegralProblem(numerical_integrand, ([arb_float(0) for _ ∈ 1:3], [arb_float(1) for _ ∈ 1:3]))\n",
    "    integral_solution = solve(integral_problem, HCubatureJL())\n",
    "    # integral_solution = solve(integral_problem, VEGASMC())\n",
    "    integral_solution.retcode == ReturnCode.Success || @warn \"Integral return code: $(integral_solution.retcode)\"\n",
    "\n",
    "    return prefactor * EU(integral_solution.u, 2)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9d6c35b7",
   "metadata": {},
   "outputs": [],
   "source": [
    "function calculate_fₕ(a, kₕ;\n",
    "    m_inflaton=GeV(arb_float(1e13)),\n",
    "    λ_inflaton_reheaton_reheaton=GeV(arb_float(1e7)),\n",
    "    solver=Rosenbrock23(),\n",
    "    number_of_CT::Int=10^6,\n",
    "    verbose_flag::Bool=true,\n",
    "    abstol=0, reltol=1e-10\n",
    ")\n",
    "    kₕ ≤ m_inflaton || return zero(arb_float)\n",
    "    # k̃ₕ = kₕ * a / a_ini\n",
    "    evolution_solution = solve_evolution_system(\n",
    "        a_fin = a,\n",
    "        m_inflaton = m_inflaton,\n",
    "        λ_inflaton_reheaton_reheaton = λ_inflaton_reheaton_reheaton,\n",
    "        initial_condition_fixing = true,\n",
    "        solver = solver,\n",
    "    )\n",
    "\n",
    "    a′_list = geomspace(kₕ * a / m_inflaton, a, number_of_CT)\n",
    "    # a′_list = geomspace(a_ini, a, number_of_CT)\n",
    "    kₕ′_list = (kₕ * a) ./ a′_list\n",
    "    verbose_flag && @info \"Calculating collision term...\"\n",
    "    CT_list = [\n",
    "        collision_term_h(a′, kₕ', m_inflaton, λ_inflaton_reheaton_reheaton, evolution_solution;\n",
    "            abstol=abstol, reltol=reltol\n",
    "        ) for (a′, kₕ′) in zip(a′_list, kₕ′_list)\n",
    "    ]\n",
    "    verbose_flag && @info \"Done for collision term calculation.\"\n",
    "\n",
    "    ρ′_total_comoving_in_EU_list = (exp ∘ last ∘ evolution_solution ∘ log).(a′_list)\n",
    "    ρ′_total_list = EU.(ρ′_total_comoving_in_EU_list, 4) ./ a′_list.^4\n",
    "    H′_list = sqrt.(ρ′_total_list ./ (3 * NU.M_Pl^2))\n",
    "    \n",
    "    integrand_list = CT_list ./ (a′_list.^4 .* H′_list)\n",
    "    @assert all(>(0), integrand_list) \"Non-positive integrand encountered in fₕ calculation: $(integrand_list)\"\n",
    "\n",
    "    integrand_interpolation = LinearInterpolation(log.(integrand_list), log.(a′_list))\n",
    "    integral_problem = IntegralProblem(\n",
    "        (a′, _) -> exp(integrand_interpolation(log(a′))),\n",
    "        (first(a′_list), last(a′_list)),\n",
    "    )\n",
    "    integral_solution = solve(integral_problem, QuadGKJL(); abstol=abstol, reltol=reltol)\n",
    "    integral_solution.retcode == ReturnCode.Success || @warn \"Integral return code: $(integral_solution.retcode)\"\n",
    "\n",
    "    return integral_solution.u\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "19346b4d",
   "metadata": {},
   "outputs": [],
   "source": [
    "m_inflaton = GeV(arb_float(1e13))\n",
    "λ = GeV(arb_float(1e7))\n",
    "a_fin = arb_float(1e8) * a_ini\n",
    "\n",
    "kₕ_list = geomspace(1e-4 * m_inflaton, 9.9e-1 * m_inflaton, 100)\n",
    "fₕ_list = calculate_fₕ.(a_fin, kₕ_list; number_of_CT=1000)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ff69d2dd",
   "metadata": {},
   "outputs": [],
   "source": [
    "figure = Figure()\n",
    "axis = Axis(figure[1, 1];\n",
    "    xlabel = L\"k_h ~ [\\mathrm{GeV}]\",\n",
    "    ylabel = L\"f_h\",\n",
    "    xscale = log10, yscale = log10,\n",
    ")\n",
    "\n",
    "figure"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.6",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
